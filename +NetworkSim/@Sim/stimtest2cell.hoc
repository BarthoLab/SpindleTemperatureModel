{load_file("nrngui.hoc")}

//----------------------------------------------------------------------------
//  Setup variables
//----------------------------------------------------------------------------

// Passed values
dt = dt_arg
v_init = v_init_arg
celsius = celsius_arg
tstop = tstop_arg

// Base settings
objref fh // file handle
synapsVectorSize = 3
steps_per_ms = 1/dt

//----------------------------------------------------------------------------
//  Setup Cells
//----------------------------------------------------------------------------

{load_file("TC.tem")}
{load_file("RE.tem")}

objectvar StartCell, EndCell, Receptor, Receptor2

// TC
EndCell = new sTC(temperature_it, temperature_ih)    
EndCell.soma.ghbar_iar = 1.5e-5  
EndCell.kl.gmax = k_leak*.01

Receptor = new GABAa_S()
Receptor2 = new GABAb_S()
EndCell.soma Receptor.loc(0.5)
EndCell.soma Receptor2.loc(0.5) 

Alpha_GABAa_S = 20		// from diffusion model
Beta_GABAa_S = 0.162
Cmax_GABAa_S = 0.5		// short pulses
Cdur_GABAa_S = 0.3
Erev_GABAa_S = -85		// Rinzel's Erev
temperature_GABAa_S = temperature_gabaa

K1_GABAb_S	= 0.09	//	(/ms mM) forward binding rate to receptor
K2_GABAb_S	= 0.0012 //	(/ms)	backward (unbinding) rate of receptor
K3_GABAb_S	= 0.18 //	(/ms)	rate of G-protein production
K4_GABAb_S	= 0.034 //	(/ms)	rate of G-protein decay
KD_GABAb_S	= 100	//		dissociation constant of K+ channel
n_GABAb_S	= 4	//		nb of binding sites of G-protein on K+
Erev_GABAb_S	= -95	//	(mV)	reversal potential (E_K)
Cmax_GABAb_S = 0.5	// short pulses
Cdur_GABAb_S = 0.3
temperature_GABAb_S = temperature_gabab


// nRT
StartCell = new sRE(temperature_it2, leak_g, cadecayTau, cac_sk, gkbar_sk)  
    
objref gabaaNetCon, gababNetCon

StartCell.soma gabaaNetCon = new NetCon(&v(0.5), Receptor)
StartCell.soma gababNetCon = new NetCon(&v(0.5), Receptor2)
Receptor.gmax = gabaa_gmax
Receptor2.gmax = gabab_gmax  

//----------------------------------------------------------------------------
//  IClamp
//----------------------------------------------------------------------------

objref stim, stim2, baseStim

objref vc
StartCell.soma vc = new SEClamp(0.5)
vc.rs = 0.01
vc.dur1 = tstop
vc.amp1= baseStimPotential

run()

StartCell.soma baseStim = new IClamp(0.5)
baseStim.del = 0
baseStim.dur = tstop
baseStim.amp = vc.i
vc.dur1 = 0.0

print vc.i

StartCell.soma stim = new IClamp(0.5)
stim.del = stimDelay
stim.dur = stimLength
stim.amp = stimAmpnRT //0.04

EndCell.soma stim2 = new IClamp(0.5)
stim2.del = stimDelay
stim2.dur = stimLength
stim2.amp = stimAmpTC

//----------------------------------------------------------------------------
//  Setup recording
//----------------------------------------------------------------------------

objref CellRecord, CellRecord2
CellRecord = new Vector()
{CellRecord.record(&StartCell.soma.v(0.5), dt)}
CellRecord2 = new Vector()
{CellRecord2.record(&EndCell.soma.v(0.5), dt)}

//----------------------------------------------------------------------------
//  RUN
//----------------------------------------------------------------------------

init()
run()

//----------------------------------------------------------------------------
//  Write to file
//----------------------------------------------------------------------------

objref tempmatrix

fh = new File()
{fh.wopen("stim_test.txt")}
tempmatrix = new Matrix()
{tempmatrix.resize(CellRecord.size(), 2)}
{tempmatrix.setcol(0, CellRecord)}
{tempmatrix.setcol(1, CellRecord2)}
{tempmatrix.fprint(0, fh, " %g")}
{fh.close}

//----------------------------------------------------------------------------
//  DONE
//----------------------------------------------------------------------------

print " DONE "